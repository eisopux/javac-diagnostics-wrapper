/*
 * javac diagnostics wrapper project build file.
 */

plugins {
    id 'java'
    id 'application'
    id 'eclipse'
    // https://plugins.gradle.org/plugin/com.gradleup.shadow
    id 'com.gradleup.shadow' version '9.3.0'
    id 'com.diffplug.spotless' version '8.1.0'
    id 'net.ltgt.errorprone' version '4.3.0'
}

repositories {
    mavenCentral()
}

dependencies {
    implementation 'com.google.code.gson:gson:2.13.2'
    testImplementation 'junit:junit:4.13.2'
    if (JavaVersion.current() >= JavaVersion.VERSION_17) {
        errorprone('com.google.errorprone:error_prone_core:2.45.0')
        errorproneJavac('com.google.errorprone:javac:9+181-r4173-1')
    }
}

application {
    // Define the main class for the application
    mainClass = 'org.eisopux.diagnostics.prebuilt.JsonDiagnostics'
}

tasks.withType(JavaCompile) { compilationTask ->
    options.compilerArgs += ['-Xlint:-options', '-Werror']
    options.errorprone.enabled = JavaVersion.current() >= JavaVersion.VERSION_17
    sourceCompatibility = 8
    targetCompatibility = 8
}

shadowJar {
}

spotless {
    java {
        googleJavaFormat().aosp()
        importOrder('com', 'jdk', 'lib', 'lombok', 'org', 'java', 'javax')
        formatAnnotations()
    }
    groovyGradle {
        target '*.gradle'
        importOrder()
        greclipse()  // which formatter Spotless should use to format .gradle files.
        indentWithSpaces(4)
        trimTrailingWhitespace()
    }
}

eclipse.classpath {
    defaultOutputDir = file("build/default")
    file.whenMerged { cp ->
        cp.entries.forEach { cpe ->
            if (cpe instanceof org.gradle.plugins.ide.eclipse.model.SourceFolder) {
                cpe.output = cpe.output.replace "bin/", "build/classes/java/"
            }
            if (cpe instanceof org.gradle.plugins.ide.eclipse.model.Output) {
                cpe.path = cpe.path.replace "bin/", "build/"
            }
        }
    }
}
