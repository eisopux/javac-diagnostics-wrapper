/*
 * javac diagnostics wrapper project build file.
 */

plugins {
    id 'java'
    id 'application'
    // https://plugins.gradle.org/plugin/com.gradleup.shadow
    id 'com.gradleup.shadow' version '8.3.6'
    id 'com.diffplug.spotless' version '7.0.2'
    id 'net.ltgt.errorprone' version '4.1.0'
}

repositories {
    mavenCentral()
}

dependencies {
    implementation 'com.google.code.gson:gson:2.13.0'
    testImplementation 'junit:junit:4.13.2'
    if (JavaVersion.current() >= JavaVersion.VERSION_17) {
        errorprone('com.google.errorprone:error_prone_core:2.36.0')
        errorproneJavac('com.google.errorprone:javac:9+181-r4173-1')
    }
}

application {
    // Define the main class for the application
    mainClass = 'io.github.wmdietl.diagnostics.json.lsp.Main'
}

tasks.withType(JavaCompile) { compilationTask ->
    options.compilerArgs += ['-Xlint:-options', '-Werror']
    options.errorprone.enabled = JavaVersion.current() >= JavaVersion.VERSION_17
    sourceCompatibility = 8
    targetCompatibility = 8
}

shadowJar {
}

spotless {
    java {
        googleJavaFormat().aosp()
        importOrder('com', 'jdk', 'lib', 'lombok', 'org', 'java', 'javax')
        formatAnnotations()
    }
    groovyGradle {
        target '*.gradle'
        importOrder()
        greclipse()  // which formatter Spotless should use to format .gradle files.
        indentWithSpaces(4)
        trimTrailingWhitespace()
    }
}
